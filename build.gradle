plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.10.0'
    id 'org.hidetake.swagger.generator' version '2.18.1'
}

group 'ba.klika'
version '1.3'

//  pugin metadata configuration
pluginBundle {
    website = 'https://github.com/klikaba/gradle-appcenter'
    vcsUrl = 'https://github.com/klikaba/gradle-appcenter'
    tags = ['appcenter', 'download', 'mobile', 'apk', 'app']
}

gradlePlugin {
    plugins {
        greetingsPlugin {
            id = 'ba.klika.appcenter'
            displayName = 'AppCenter'
            description = 'Plugin for downloading app builds from AppCenter'
            implementationClass = 'ba.klika.AppcenterPlugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.12'
//    compile 'org.spockframework:spock-core:1.3-groovy-2.4'
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.9' // Swagger Codegen V2
}

swaggerSources {
    appcenter {
        inputFile = file('src/main/swagger/appcenter.json')
        code {
            language = 'groovy'
            configFile = file('gradle/swagger/appcenter-config.json')
            // Validate YAML before code generation. for Swagger Codegen V2 / V3
            //dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.appcenter.code
sourceSets.main.java.srcDir "${swaggerSources.appcenter.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.appcenter.code.outputDir}/src/main/resources"